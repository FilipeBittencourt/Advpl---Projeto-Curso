#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"
#INCLUDE "RWMAKE.CH"

/*/{Protheus.doc} RES_REPRE
@author Bruno Madaleno
@since 07/01/10
@version 1.0
@description Rotina para Rescisão do Representante
@type function
/*/

USER FUNCTION RES_REPRE()
	Local I
	PRIVATE SQL 	:= ""
	PRIVATE ENTER 	:= CHR(13)+CHR(10)
	Private lFiltra	:= .F. //Define se irá filtras os registros na tabela SE1 e SE3 por Marca
	Private nMarca	:= ""  //Marca de acordo com o parametro MV_PAR04 e a variavel cEmpAnt

	//carrega as peguntas do relatório Rescisão
	Pergunte("REL_RESCI", .F.)
	nPAR07 := MV_PAR07
	nPAR08 := MV_PAR08

	//carrega as perguntas do programa original
	nLastKey := Pergunte("RESCREP", .T.)
	IF !nLastKey
		Return()
	ENDIF

	//Grava o parametro do relatório na rotina
	MV_PAR02 := nPAR07
	MV_PAR03 := nPAR08

	//Bloqueia o uso da Marca nas empresas Biancogres, Incesa e Vitcer
	If cEmpAnt $ "01_05_14" .And. !Empty(Alltrim(MV_PAR04))
		MsgBox("O parâmetro MARCA não pode ser utilizado nesta empresa!","RES_REPRE","STOP")	
		Return()
	EndIf

	//Para Empresa LM, pode realizar a pesquisa/filtro por Marca 
	If cEmpAnt == "07"
		lFiltra := .T.
		If MV_PAR04 == "0101"
			nMarca := "1"
		ElseIf MV_PAR04 == "0501"
			nMarca := "2"
		ElseIf MV_PAR04 == "0599"
			nMarca := "3"
		ElseIf MV_PAR04 == "1399"
			nMarca := "4"
		ElseIf MV_PAR04 == "0199"
			nMarca := "6"			
		ElseIf MV_PAR04 == "1302"
			nMarca := "7"	
		Else
			MsgAlert("Marca não configurada para processo de rescisão nesta empresa. Entre em contato com a TI!")
			Return()		
		EndIf	
	EndIf

	//Tabela de Rescisão de Representante
	Z78->(DbSetOrder(2)) //Representante+Marca
	If Z78->(DbSeek(xFilial("Z78")+MV_PAR01+MV_PAR04))
		MsgBox("Rescisão já foi realizada para este Representante nesta Marca!","RES_REPRE","STOP")	
		Return()
	Else

		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		// *********************** GRAVA A DATA DO REPRESENTANTE **************************
		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		RecLock("Z78",.T.)
		Z78->Z78_FILIAL	:= xFilial("Z78")	//Representante
		Z78->Z78_VEND	:= MV_PAR01  		//Representante
		Z78->Z78_DTRESC	:= dDataBase 		//Data Base para fechamanto
		Z78->Z78_OBS	:= MV_PAR02
		Z78->Z78_VALOR	:= MV_PAR03
		Z78->Z78_MARCA	:= MV_PAR04  		//Marca    
		Z78->(MsUnlock())		
		Z78->(DbSkip())

		//Posiciona no Representante
		SA3->(DbSetOrder(1))
		SA3->(DbSeek(xFilial("SA3")+MV_PAR01))

		//Define o Vendededor Padrao para a Empresa
		If cEmpAnt == "05"
			nVend := "000258"	
		Else
			nVend := "999999"
		EndIF

		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		// ******* ACERTO PEDIDO VENDA ******
		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		//Backup do Vendedor 1, Comisssao 1, Perc 1
		SQL := "UPDATE "+RETSQLNAME("SC6")+" SET C6_YVENDRC = C5_VEND1, C6_YVLCREC = ((((C6_QTDVEN-C6_QTDENT) * C6_PRCVEN) / 100)*C6_COMIS1), C6_YPCREC = C6_COMIS1 " + ENTER
		SQL += "FROM "+RETSQLNAME("SC6")+" SC6, "+RETSQLNAME("SC5")+" SC5 " + ENTER
		SQL += "WHERE	C5_FILIAL 		= '"+xFilial("SC5")+"'	AND " + ENTER
		SQL += "		C6_FILIAL 		= '"+xFilial("SC6")+"'	AND " + ENTER
		SQL += "		C5_NUM 			= C6_NUM				AND " + ENTER
		SQL += "		C5_VEND1 		= '"+MV_PAR01+"'		AND " + ENTER
		If lFiltra
			SQL += "		C5_YEMP 		= '"+MV_PAR04+"'		AND " + ENTER
		EndIf	
		SQL += "		C6_PRODUTO 		>= 'A' 		AND " + ENTER
		SQL += "		C6_QTDVEN-C6_QTDENT > 0 	AND " + ENTER
		SQL += "		C6_BLQ 			<> 'R' 		AND " + ENTER
		SQL += "		SC6.D_E_L_E_T_	= '' 		AND " + ENTER
		SQL += "		SC5.D_E_L_E_T_ 	= '' 			" + ENTER
		U_BIAMsgRun("Aguarde... Processando Rescisão... Pedidos de Venda",,{|| TcSQLExec(Sql)})

		//Backup do Vendedor 2, Comisssao 2, Perc 2 / Vendedor 3, Comisssao 3, Perc 3 / Vendedor 4, Comisssao 4, Perc 4 / Vendedor 5, Comisssao 5, Perc 5
		For I := 2 To 5
			SQL := "UPDATE "+RETSQLNAME("SC6")+" SET C6_YVENDR"+ALLTRIM(STR(I))+" = C5_VEND"+ALLTRIM(STR(I))+", C6_YVLCRE"+ALLTRIM(STR(I))+" = ((((C6_QTDVEN-C6_QTDENT) * C6_PRCVEN) / 100)*C6_COMIS"+ALLTRIM(STR(I))+"), C6_YPCREC"+ALLTRIM(STR(I))+" = C6_COMIS"+ALLTRIM(STR(I))+" " + ENTER
			SQL += "FROM "+RETSQLNAME("SC6")+" SC6, "+RETSQLNAME("SC5")+" SC5 " + ENTER
			SQL += "WHERE	C5_FILIAL 		= '"+xFilial("SC5")+"'	AND " + ENTER
			SQL += "		C6_FILIAL 		= '"+xFilial("SC6")+"'	AND " + ENTER
			SQL += "		C5_NUM 			= C6_NUM				AND " + ENTER
			SQL += "		C5_VEND"+ALLTRIM(STR(I))+" = '"+MV_PAR01+"' AND " + ENTER
			If lFiltra
				SQL += "		C5_YEMP 		= '"+MV_PAR04+"'			AND " + ENTER 
			EndIf
			SQL += "		C6_PRODUTO 		>= 'A' 		AND " + ENTER
			SQL += "		C6_QTDVEN-C6_QTDENT > 0 	AND " + ENTER
			SQL += "		C6_BLQ 			<> 'R' 		AND " + ENTER
			SQL += "		SC6.D_E_L_E_T_	= '' 		AND " + ENTER
			SQL += "		SC5.D_E_L_E_T_ 	= '' 			" + ENTER
			U_BIAMsgRun("Aguarde... Processando Rescisão... Pedidos de Venda",,{|| TcSQLExec(Sql)})
		Next

		//Zerando Comissao 1, 2, 3, 4 e 5
		For I := 1 To 5
			SQL := "UPDATE "+RETSQLNAME("SC6")+" SET C6_COMIS"+ALLTRIM(STR(I))+" = 0 " + ENTER
			SQL += "FROM "+RETSQLNAME("SC6")+" SC6, "+RETSQLNAME("SC5")+" SC5 " + ENTER
			SQL += "WHERE	C5_FILIAL 		= '"+xFilial("SC5")+"'	AND " + ENTER
			SQL += "		C6_FILIAL 		= '"+xFilial("SC6")+"'	AND " + ENTER
			SQL += "		C5_NUM 			= C6_NUM				AND " + ENTER
			SQL += "	    C5_VEND"+ALLTRIM(STR(I))+" = '"+MV_PAR01+"' AND " + ENTER
			If lFiltra
				SQL += "		C5_YEMP 		= '"+MV_PAR04+"'			AND " + ENTER
			EndIf
			SQL += "		C6_PRODUTO 		>= 'A' 		AND " + ENTER
			SQL += "		C6_QTDVEN-C6_QTDENT > 0 	AND " + ENTER
			SQL += "		C6_BLQ 			<> 'R' 		AND " + ENTER
			SQL += "		SC6.D_E_L_E_T_ 	= '' 		AND " + ENTER
			SQL += "		SC5.D_E_L_E_T_ 	= '' 			" + ENTER
			U_BIAMsgRun("Aguarde... Processando Rescisão... Pedidos de Venda",,{|| TcSQLExec(Sql)})
		Next

		//Zerando Vendedor 1, 2, 3, 4 e 5 
		For I := 1 To 5
			SQL := "UPDATE "+RETSQLNAME("SC5")+" SET C5_VEND"+ALLTRIM(STR(I))+" = '"+nVend+"' " + ENTER
			SQL += "FROM "+RETSQLNAME("SC6")+" SC6, "+RETSQLNAME("SC5")+" SC5 " + ENTER
			SQL += "WHERE	C5_FILIAL 		= '"+xFilial("SC5")+"'	AND " + ENTER
			SQL += "		C6_FILIAL 		= '"+xFilial("SC6")+"'	AND " + ENTER
			SQL += "		C5_NUM 			= C6_NUM				AND " + ENTER
			SQL += "		C5_VEND"+ALLTRIM(STR(I))+" = '"+MV_PAR01+"' AND " + ENTER
			If lFiltra
				SQL += "		C5_YEMP 		= '"+MV_PAR04+"'			AND " + ENTER 
			EndIf		
			SQL += "		C6_PRODUTO		>= 'A' 		AND " + ENTER
			SQL += "		C6_QTDVEN-C6_QTDENT > 0 	AND " + ENTER
			SQL += "		C6_BLQ 			<> 'R' 		AND " + ENTER
			SQL += "		SC6.D_E_L_E_T_ 	= '' 		AND " + ENTER
			SQL += "		SC5.D_E_L_E_T_	= '' 			" + ENTER
			U_BIAMsgRun("Aguarde... Processando Rescisão... Pedidos de Venda",,{|| TcSQLExec(Sql)})
		Next

		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		// ***** ACERTO TITULOS A RECEBER *****
		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		For I := 1 To 5
			//Selecionando Titulos em Aberto 
			SQL := "SELECT SE1.R_E_C_N_O_ " + ENTER
			SQL += "FROM "+RETSQLNAME("SE1")+" SE1, "+RetSqlName("SA1")+" SA1 " + ENTER
			SQL += "WHERE	SE1.E1_FILIAL  = '"+xFilial("SE1")+"'	AND " + ENTER 
			SQL += "		SA1.A1_COD     = SE1.E1_CLIENTE		AND " + ENTER
			SQL += "		SA1.A1_LOJA    = SE1.E1_LOJA		AND " + ENTER
			SQL += "		SE1.E1_SALDO   > 0 					AND " + ENTER
			SQL += "		SE1.E1_TIPO	   NOT IN ('RA','BOL')	AND " + ENTER //EM 20/05/13 - VAGNER
			SQL += "		SA1.A1_YCALCCM <> 'N'				AND " + ENTER
			SQL += "	   	SE1.E1_VEND"+ALLTRIM(STR(I))+" = '"+MV_PAR01+"' AND  " + ENTER
			If lFiltra
				SQL += "	   	SE1.E1_PREFIXO LIKE '%"+nMarca+"%'		AND  " + ENTER
			EndIf
			SQL += "		SE1.D_E_L_E_T_ = '' 				AND " + ENTER
			SQL += "		SA1.D_E_L_E_T_ = '' 					" + ENTER
			If chkfile("_SE1")
				dbSelectArea("_SE1")
				dbCloseArea()
			EndIf
			TcQuery SQL ALIAS "_SE1" NEW
			dbSelectArea("_SE1")
			dbGoTop()

			//Limpa tabela com os Titulos a Receber
			TcSqlExec("DELETE FROM TMP_RES_REPRE_TITULOS_VENCER")

			//Utiliza funcao padrao para calcular a comissão e gravar os titulos
			While !_SE1->(EOF())
				dbSelectArea("SE1")
				dbGoTo(_SE1->R_E_C_N_O_)
				aBases	:= U_fCalcComiss(SE1->E1_FILIAL,SE1->E1_PREFIXO,SE1->E1_NUM,SE1->E1_PARCELA,SE1->E1_TIPO,I)
				cSQL 	:= "INSERT INTO TMP_RES_REPRE_TITULOS_VENCER VALUES ("+Alltrim(Str(_SE1->R_E_C_N_O_))+","+Alltrim(Str(aBases[1]))+","+Alltrim(Str(aBases[2]/aBases[1]*100))+","+Alltrim(Str(aBases[2]))+")"
				TcSqlExec(cSQL)
				_SE1->(dbSkip())
			End	

			If I == 1
				//Backup do Vendedor 1, Comisssao 1, Perc 1
				SQL := "UPDATE "+RETSQLNAME("SE1")+" SET  E1_YVENDRC = E1_VEND1, E1_YVLCREC = ((E1_SALDO*PERC_COMIS)/100), E1_YPCREC  = PERC_COMIS " + ENTER
				SQL += "FROM "+RETSQLNAME("SE1")+" SE1, TMP_RES_REPRE_TITULOS_VENCER TMP " + ENTER
				SQL += "WHERE	SE1.R_E_C_N_O_ = TMP.R_E_C_N_O_ AND " + ENTER
				SQL += "		SE1.E1_SALDO   > 0 AND " + ENTER
				SQL += "	    SE1.E1_VEND1 = '"+MV_PAR01+"' AND " + ENTER
				If lFiltra
					SQL += "	   	SE1.E1_PREFIXO LIKE '%"+nMarca+"%'		AND  " + ENTER
				EndIf
				SQL += "		SE1.D_E_L_E_T_ = '' " + ENTER
				U_BIAMsgRun("Aguarde... Processando Rescisão... Titulos a Receber",,{|| TcSQLExec(Sql)})
			Else
				//Backup do Vendedor 2, Comisssao 2, Perc 2 / Vendedor 3, Comisssao 3, Perc 3 / Vendedor 4, Comisssao 4, Perc 4 / Vendedor 5, Comisssao 5, Perc 5
				SQL := "UPDATE "+RETSQLNAME("SE1")+" SET  E1_YVENDR"+ALLTRIM(STR(I))+" = E1_VEND"+ALLTRIM(STR(I))+", E1_YVLCRE"+ALLTRIM(STR(I))+" = ((E1_SALDO*PERC_COMIS)/100), E1_YPCREC"+ALLTRIM(STR(I))+" = PERC_COMIS " + ENTER
				SQL += "FROM "+RETSQLNAME("SE1")+" SE1, TMP_RES_REPRE_TITULOS_VENCER TMP " + ENTER
				SQL += "WHERE	SE1.R_E_C_N_O_ = TMP.R_E_C_N_O_ AND " + ENTER
				SQL += "		SE1.E1_SALDO   > 0 AND " + ENTER
				SQL += "	    SE1.E1_VEND"+ALLTRIM(STR(I))+" = '"+MV_PAR01+"' AND " + ENTER
				If lFiltra
					SQL += "	   	SE1.E1_PREFIXO LIKE '%"+nMarca+"%'		AND  " + ENTER
				EndIf
				SQL += "		SE1.D_E_L_E_T_ = '' " + ENTER
				U_BIAMsgRun("Aguarde... Processando Rescisão... Titulos a Receber",,{|| TcSQLExec(Sql)})
			EndIf
		Next

		//Zerando Vendedor 1, Perc 1, Comissao 1 / Vendedor 2, Perc 2, Comissao 2 / Vendedor 3, Perc 3, Comissao 3 / Vendedor 4, Perc 4, Comissao 4 / Vendedor 5, Perc 5, Comissao 5
		For I := 1 To 5
			SQL := "UPDATE "+RETSQLNAME("SE1")+" SET E1_VEND"+ALLTRIM(STR(I))+" = '"+nVend+"', E1_COMIS"+ALLTRIM(STR(I))+" = 0, E1_VALCOM"+ALLTRIM(STR(I))+" = 0 " + ENTER
			SQL += "FROM "+RETSQLNAME("SE1")+" " + ENTER
			SQL += "WHERE	E1_FILIAL	= '"+xFilial("SE1")+"'	AND " + ENTER 
			SQL += "		E1_SALDO	> 0 					AND " + ENTER
			SQL += "		E1_VEND"+ALLTRIM(STR(I))+" = '"+MV_PAR01+"' AND " + ENTER
			If lFiltra
				SQL += "		E1_PREFIXO	LIKE '%"+nMarca+"%'			AND  " + ENTER
			EndIf
			SQL += "		D_E_L_E_T_ = '' " + ENTER
			U_BIAMsgRun("Aguarde... Processando Rescisão... Titulos a Receber",,{|| TcSQLExec(Sql)})
		Next

		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		// ***** ACERTO COMISSAO *****
		// ±±±±±±±±±±±±±±±±±±±±±±±±±±±±
		//Backup do Vendedor, Comisssao, Perc 
		SQL := "UPDATE "+RETSQLNAME("SE3")+" SET E3_YVENDRC = E3_VEND, E3_YVLCREC = E3_COMIS, E3_YPCREC = E3_PORC " + ENTER
		SQL += "FROM "+RETSQLNAME("SE3")+" 				" + ENTER
		SQL += "WHERE	E3_FILIAL	= '"+xFilial("SE3")+"'	AND " + ENTER 
		SQL += "		E3_VEND		= '"+MV_PAR01+"' 		AND " + ENTER
		If lFiltra
			SQL += "		E3_PREFIXO	LIKE '%"+nMarca+"%'			AND  " + ENTER
		EndIf
		SQL += "		E3_DATA = '' AND 				" + ENTER
		SQL += "		D_E_L_E_T_ = '' 				" + ENTER
		U_BIAMsgRun("Aguarde... Processando Rescisão... Comissão",,{|| TcSQLExec(Sql)})

		//Zerando Vendedor, Comisssao, Perc 
		SQL := "UPDATE "+RETSQLNAME("SE3")+" SET E3_VEND = '"+nVend+"', E3_PORC = 0, E3_COMIS = 0 " + ENTER
		SQL += "FROM "+RETSQLNAME("SE3")+" 					" + ENTER
		SQL += "WHERE	E3_FILIAL	= '"+xFilial("SE3")+"'	AND " + ENTER 
		SQL += "		E3_VEND		= '"+MV_PAR01+"' 		AND " + ENTER
		If lFiltra
			SQL += "		E3_PREFIXO	LIKE '%"+nMarca+"%'			AND  " + ENTER
		EndIf
		SQL += "		E3_DATA 	= '' 				AND " + ENTER
		SQL += "		D_E_L_E_T_ 	= '' 					" + ENTER	
		U_BIAMsgRun("Aguarde... Processando Rescisão... Comissão",,{|| TcSQLExec(Sql)})

	ENDIF

	// ±±±±±±±±±±±±±±±±±±±±±±±±±±
	// ***** ACERTO CLIENTE *****
	// ±±±±±±±±±±±±±±±±±±±±±±±±±±
	//Depois de executar todos os updates acima, o sistema volta a informacao no cadastro de cliente.
	SQL := "UPDATE "+RetSqlName("SA1")+" SET A1_YCALCCM = 'S' 	" + ENTER	
	SQL += " WHERE A1_YCALCCM = 'N' AND 						" + ENTER	
	SQL += "	  (A1_VEND    = '"+MV_PAR01+"' OR A1_YVENDB2 = '"+MV_PAR01+"' OR A1_YVENDB3 = '"+MV_PAR01+"' OR	" + ENTER	
	SQL += "	   A1_YVENDI  = '"+MV_PAR01+"' OR A1_YVENDI2 = '"+MV_PAR01+"' OR A1_YVENDI3 = '"+MV_PAR01+"' OR	" + ENTER	
	SQL += "	   A1_YVENBE1 = '"+MV_PAR01+"' OR A1_YVENBE2 = '"+MV_PAR01+"' OR A1_YVENBE3 = '"+MV_PAR01+"' OR	" + ENTER	
	SQL += "	   A1_YVENML1 = '"+MV_PAR01+"' OR A1_YVENML2 = '"+MV_PAR01+"' OR A1_YVENML3 = '"+MV_PAR01+"' OR	" + ENTER	
	SQL += "	   A1_YVENVT1 = '"+MV_PAR01+"' OR A1_YVENVT2 = '"+MV_PAR01+"' OR A1_YVENVT3 = '"+MV_PAR01+"' OR	" + ENTER 
	SQL += "	   A1_YVENVI1 = '"+MV_PAR01+"' OR A1_YVENPEG = '"+MV_PAR01+"' )									" + ENTER
	SQL += "	   AND D_E_L_E_T_ = ''																			" + ENTER	
	U_BIAMsgRun("Aguarde... Processando Rescisão... Cliente",,{|| TcSQLExec(Sql)})

	MsgBox("Rotina finalizada com sucesso!","RES_REPRE","INFO")	
	MsgBox("Não esqueça de executar a rotina em todas as empresas com rescisão!","RES_REPRE","ALERT")	

RETURN()        
