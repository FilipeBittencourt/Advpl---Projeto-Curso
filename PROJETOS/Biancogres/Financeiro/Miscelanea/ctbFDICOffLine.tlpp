#include "totvs.ch"

/*/{Protheus.doc} u_ctbFIDCRec
@author Marinaldo de Jesus
@since 26/02/2021
@project FIDC
@version 1.0
@description Contabilização FIDC OffLine (Receber)
@type Procedure
/*/
function u_ctbFIDCRec()
    return(u_ctbFIDCOffLine(1))

/*/{Protheus.doc} u_ctbFIDCPag
@author Marinaldo de Jesus
@since 26/02/2021
@project FIDC
@version 1.0
@description Contabilização FIDC OffLine (Pagar)
@type Procedure
/*/
function u_ctbFIDCPag()
    return(u_ctbFIDCOffLine(2))

/*/{Protheus.doc} ctbFIDCOffLine
@author Marinaldo de Jesus
@since 26/02/2021
@project FIDC
@version 1.0
@description Contabilização FIDC OffLine
@type Procedure
/*/
procedure u_ctbFIDCOffLine(nType as numeric)
    local aArea as array
    aArea:=getArea()
    private cCadastro as character
    cCadastro:="Contabilização FIDC OffLine"
    if (!empty(nType).or.(getCTBFIDCParameters(@nType)))
        if (nType==1)
            cCadastro+=" :: RECEBER"
        elseif (nType==2)
            cCadastro+=" :: PAGAR"
        endif
        FIDC():ctbFIDCOffLine(nType)
    endif
    restArea(aArea)
    return

static function getCTBFIDCParameters(nType as numeric) as logical
    
    local aArea     as array

    local aParamBox as array
    local aParamRet as array

    local cType as character
    local cParamTit as character
    
    local lParamBox as logical
    
    aArea:=getArea()

    saveInter()

    aParamBox:=array(0)

    cType:=space(100)
    aAdd(aParamBox,{2,"Origem",@cType,{"1-FIDC Receber","2-FIDC Pagar"},100,".T.",.T.})
    
    cParamTit:="Opção Contabilização FIDC"
    lParamBox:=ParamBox(@aParamBox,@cParamTit,@aParamRet,/*bOk*/,/*aButtons*/,.T./*lCentered*/,/*nPosx*/,/*nPosy*/,/*oDlgWizard*/,/*cLoad*/,.T./*lCanSave*/,.T./*lUserSave*/)
    if (lParamBox)
        cType:=left(aParamRet[1],1)
    endif

    nType:=val(cType)

    restInter()

    restArea(aArea)

    return(lParamBox)
