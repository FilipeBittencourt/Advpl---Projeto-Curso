#include "totvs.ch"
#include "dbstruct.ch"

/*/{Protheus.doc} FIDC
@author Marinaldo de Jesus
@since 08/03/2021
@project FIDC
@version 1.0
@description Function para Impressao do Relatorio (Excel) Titulo Fornecedor FIDC
@type function
/*/
function u_FIDCPTitRel()
    return(u_FIDCPagarRelatorioTituloFornecedor())

/*/{Protheus.doc} FIDC
@author Marinaldo de Jesus
@since 08/03/2021
@project FIDC
@version 1.0
@description Procedure para Impressao do Relatorio (Excel) Titulo Fornecedor FIDC
@type procedure
/*/
procedure u_FIDCPagarRelatorioTituloFornecedor()

	local aArea				as array
	local aParamRet			as array

	local bAction			as block

	local lParamBox			as logical

	local otMultProcess		as object

    private cCadastro as character
    cCadastro:=OemToAnsi("Relatorio Titulo Fornecedor FIDC")

    aArea:=getArea()

	begin sequence

        lParamBox:=FIDCPagarTituloFornecedorParameters(@aParamRet)
        if (!lParamBox)
            break
        endif

		bAction:={||FIDCPagarRelatorioTituloFornecedor(otMultProcess,aParamRet)}
		otMultProcess:=tMultProcess():New(bAction,&("cCadastro"),OemToAnsi("Aguarde. Gerando Relatorio..."),nil,2)
		otMultProcess:Activate()

    end sequence

	restArea(aArea)

    return

static procedure FIDCPagarRelatorioTituloFornecedor(otMultProcess as object,aParamRet as array)
    EvalBlock():EvalBlock({||FIDCPagarRelatorioTituloFornecedorQuery(otMultProcess,aParamRet)},nil,.T.)
    return

static procedure FIDCPagarRelatorioTituloFornecedorQuery(otMultProcess as object,aParamRet as array)

    local aFWTTables    as array

    local bProcess      as block
    local bMsAguarde    as block

    local cTmpAlias     as character

    local cXMLPath      as character
    local cXMLFile      as character
    local cExcelTitle   as character

    local cServerIP     as character
    local cEnvServer    as character

    local lDev          as logical
    local lPicture      as logical
    local lX3Titulo     as logical
    local ltxtEditMemo  as logical

    local nTable        as numeric
    local nTables       as numeric
  
    local nIncRegua     as numeric
    
    otMultProcess:IncRegua(1,"Obtendo dados no SGBD. Aguarde...")

    otMultProcess:SetRegua(1,0)
    otMultProcess:SetRegua(2,0)

    bMsAguarde:={||cTmpAlias:=QueryView(@aParamRet,@aFWTTables)}
    MsAguarde(bMsAguarde,"Obtendo dados no SGBD. Aguarde...")
    
    nIncRegua:=(cTmpAlias)->(RecCount())

    cXMLPath:="\FIDC\XML\"
    if (dirtools():MakeDir(cXMLPath))

        cXMLFile:=A35():getXMLFile(&("cEmpAnt"),&("cFilAnt"),cXMLPath,"FIDCPagarRelatorioTituloFornecedor","","")
        
        if (type("cCadastro")=="C")
            cExcelTitle:=&("cCadastro")
            cExcelTitle+=" :: "
        else
            cExcelTitle:=""
        endif

        cExcelTitle+="Empresa: "+&("cEmpAnt")
        cExcelTitle+=" :: "
        cExcelTitle+="Filial:  "+&("cFilAnt")
        
        cServerIP:=getServerIP()
        cEnvServer:=upper(getEnvServer())

        lDev:=((cServerIP=="192.168.20.18").or.("DEV"$cEnvServer))
        if (lDev)
            cExcelTitle+=" :: EMITIDO EM AMBIENTE DE DESENVOLVIMENTO ::"    
        endif

        lPicture:=.T.
        lX3Titulo:=.T.
        ltxtEditMemo:=.F.

        otMultProcess:SetRegua(1,0)
        otMultProcess:SetRegua(2,nIncRegua)

        bProcess:={|otMultProcess|otMultProcess:IncRegua(1,"Gerando Planilha Excel. Aguarde..."),otMultProcess:IncRegua(2,"Processando...")}

        uToXML():setXMLVar("PROCESS","pEval",otMultProcess)
        uToXML():setXMLVar("PROCESS","bEval",bProcess)

        bMsAguarde:={||uToXML():QryToXML(cTmpAlias,@cXMLFile,@cExcelTitle,@lPicture,@lX3Titulo,@ltxtEditMemo)}
        MsAguarde(bMsAguarde,"Gerando Planilha Excel. Aguarde...")
        
        uToXML():clearXMLVar()

    endif

    if (valtype(aFWTTables)=="A")
        nTables:=len(aFWTTables)
        for nTable:=1 to nTables
            if (valType(aFWTTables[nTable])=="O")
                aFWTTables[nTable]:Delete()
                aFWTTables[nTable]:=nil
            endif
        next nTable
        aSize(aFWTTables,0)
    endif

    return

static function FIDCPagarTituloFornecedorParameters(aParamRet as array) as logical
    local lParamBox as logical
    lParamBox:=FIDC():getPagParamTit(@aParamRet)
    return(lParamBox)

static function QueryView(aParamRet as character,aFWTTables as array) as character
    local cTmpAlias as character
    cTmpAlias:=FIDC():getPagQueryTit(@aParamRet,@aFWTTables)
    return(cTmpAlias)
