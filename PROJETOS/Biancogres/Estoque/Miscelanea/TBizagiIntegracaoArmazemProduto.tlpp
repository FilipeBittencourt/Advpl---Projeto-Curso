#INCLUDE "PROTHEUS.CH"
#INCLUDE "TOPCONN.CH"

/*/{Protheus.doc} TBizagiIntegracaoArmazemProduto
@author Gabriel Rossi Mafioletti (Facile)
@since 12/11/2020  
@version 1.0
@description 
@type function
/*/

User Function BIPRARPD()

    Local oObj := Nil
    
    //RpcSetEnv('01','01')
    
    oObj := TBizagiIntegracaoArmazemProduto():New()

    oObj:Processa()

    //RpcClearEnv()

Return


Class TBizagiIntegracaoArmazemProduto From LongClassName

    PUBLIC Data aErro
    Data cEmail

    Public Method New() Constructor
    Public Method Processa()

    Public Method Status(cStatus, nId, cLog, cProd, cLocal)
    Public Method LogMail()

EndClass

Method New(lJob) Class TBizagiIntegracaoArmazemProduto

    ::aErro:= {}
    ::cEmail := U_GetBiaPar("MV_EMAILINTBIZARM", "sistemas.ti@biancogres.com.br")

Return(Self)

Method Processa() Class TBizagiIntegracaoArmazemProduto

    Local _cAlias := GetNextAlias()
    Local _cSql	:=	""
    Local _cClsAnt	:=	""
    Local _cPolAnt	:=	""
    Local _oMd	:=	TBiaControleMD():New()
  
    BeginSql Alias _cALias
    	%NoParser%
    	SELECT *, LOCAL AS ALMOX
    	FROM BZINTEGRACAO_ARMAZEM_PRODUTO
    	WHERE STATUS = 'A'
    	ORDER BY ID
    
    EndSql

    DBSelectArea("ZCN")
    ZCN->(dbSetOrder(2))

    While !(_cAlias)->(EOF())
   		Begin Transaction
	    	If ZCN->(DbSeek(xFilial("ZCN")+(_cAlias)->(PRODUTO+ALMOX)))
	    		_cClsAnt	:=	ZCN->ZCN_CLASSI
	    		_cPolAnt	:=	ZCN->ZCN_POLIT
	    		Reclock("ZCN",.F.)
	    		ZCN->ZCN_POLIT	:=	(_cAlias)->POLITICA
	    		ZCN->ZCN_PONPED	:=	(_cAlias)->PONTOPEDIDO
	    		ZCN->ZCN_ESTSEG	:=	(_cAlias)->ESTOQUESEG
	    		ZCN->ZCN_LE		:=	(_cAlias)->LOTEECONOMICO
	    		ZCN->ZCN_CLASSI	:=	(_cAlias)->CLASSIFICACAO
	    		ZCN->ZCN_TAG	:=	(_cAlias)->TAG
	    		If Alltrim((_cAlias)->POLITICA) $ '6/8'
	    			ZCN->ZCN_MD	:=	'N'
	    		EndIf
	    		ZCN->(MsUnlock())
	    		
	    		If Alltrim(_cPolAnt) == "4" .And. Alltrim(ZCN->ZCN_POLIT) <> '4'
	    			_oMd:ZeraMd(ZCN->ZCN_COD, ZCN->ZCN_LOCAL)
	    		EndIf
	    		
	    		If Alltrim(ZCN->ZCN_CLASSI) == '3'
	    			_cSql	:=	" UPDATE " + RetSqlName("ZCN")
	    			_cSql	+=	" SET ZCN_CLASSI = '3'"
	    			_cSql	+=	" 	,ZCN_OBSOLE = 'S'"
	    			_cSql	+=	" WHERE ZCN_COD = " + ValtoSql((_cAlias)->PRODUTO)
	    			_cSql	+=	" 	AND D_E_L_E_T_ = '' "
	    			
	    			TcSqlExec(_cSql)
	 
	    			_cSql	:=	" UPDATE " + RetSqlName("SB1")
	    			_cSql	+=	" SET B1_YSTATUS = '3'"
	    			_cSql	+=	" WHERE B1_COD = " + ValtoSql((_cAlias)->PRODUTO)
	    			_cSql	+=	" 	AND D_E_L_E_T_ = '' "
	    			
	    			TcSqlExec(_cSql)
	 
	    		ElseIf Alltrim(_cClsAnt) == "3" 

	    			_cSql	:=	" UPDATE " + RetSqlName("ZCN")
	    			_cSql	+=	" SET ZCN_CLASSI = " + ValtoSql(ZCN->ZCN_CLASSI)
	    			_cSql	+=	" 	,ZCN_OBSOLE = 'N'"
	    			_cSql	+=	" WHERE ZCN_COD = " + ValtoSql((_cAlias)->PRODUTO)
	    			_cSql	+=	" 	AND D_E_L_E_T_ = '' "
	    			
	    			TcSqlExec(_cSql)
	 
	    			_cSql	:=	" UPDATE " + RetSqlName("SB1")
	    			_cSql	+=	" SET B1_YSTATUS = '1'"
	    			_cSql	+=	" WHERE B1_COD = " + ValtoSql((_cAlias)->PRODUTO)
	    			_cSql	+=	" 	AND D_E_L_E_T_ = '' "
	    			
	    			TcSqlExec(_cSql)
	 
	    		EndIf
	 
	    		::Status("P", (_cAlias)->ID)
	    		
	    	Else
	    		::Status("E", (_cAlias)->ID,"Produto/Armazém não encontrado(s)",(_cAlias)->PRODUTO,(_cAlias)->ALMOX)
	    		
	    	EndIf
    	END TRANSACTION
    	(_cAlias)->(DbSkip())
    EndDo

    (_cAlias)->(DbCloseArea())

    ::LogMail()

Return()

Method Status(cStatus, nId, cLog, cProd, cLocal) Class TBizagiIntegracaoArmazemProduto

    Local lRet := .T.
    Local cSql := ""

    Default cStatus := "" // E=Erro; P=Processado; A=Aguard. Processamento
    Default cLog    := ""
    Default cProd 	:= ""
    Default cLocal 	:= ""
    Default nId     := 0

    If cStatus == "E"

        aAdd(::aErro, {nId,cProd,cLocal, cLog})

    EndIf

    cSql := " UPDATE dbo.BZINTEGRACAO_ARMAZEM_PRODUTO SET "

    cSql += " STATUS   = " + ValToSql(cStatus)
    cSql += " , LOG    = " + ValToSql(cLog)
    
    cSql += " , DTINTEGRA = " + ValToSql(DTOS(dDataBase))
    cSql += " , HRINTEGRA = CONVERT(VARCHAR(8), GETDATE(), 114) "

    cSql += " WHERE ID = " + cValToChar(nId)

    lRet := TcSQLExec(cSql) == 0

    If !lRet

        ConOut("TBizagiIntegracaoArmazemProduto: TCSQLError() -> " + TCSQLError())

    EndIf

Return(lRet)

Method LogMail() Class TBizagiIntegracaoArmazemProduto

    Local nW    := 0
    Local lFound := .F.
    Local nId	:=	0
    Local cHtml := ""
    Local oMail := TAFMail():New()

    For nW := 1 To Len(::aErro)

    	If nId <> ::aErro[nW,1]

		    If nId <> 0
		        cHtml += '</tbody>'
		        cHtml += '</table>'
		
		    EndIf
		
		    nId := ::aErro[nW,1]
		
		
		    cHtml += '<style type="text/css">'
		    cHtml += '.tg  {border-collapse:collapse;border-color:#aaa;border-spacing:0;}'
		    cHtml += '.tg td{background-color:#fff;border-color:#aaa;border-style:solid;border-width:1px;color:#333;'
		    cHtml += '  font-family:Arial, sans-serif;font-size:14px;overflow:hidden;padding:10px 5px;word-break:normal;}'
		    cHtml += '.tg th{background-color:#f38630;border-color:#aaa;border-style:solid;border-width:1px;color:#fff;'
		    cHtml += '  font-family:Arial, sans-serif;font-size:14px;font-weight:normal;overflow:hidden;padding:10px 5px;word-break:normal;}'
		    cHtml += '.tg .tg-zw5y{border-color:inherit;text-align:center;text-decoration:underline;vertical-align:top}'
		    cHtml += '.tg .tg-0lax{text-align:left;vertical-align:top}'
		    cHtml += '</style>'
		
		    cHtml += '<table width="100%" class="tg">'
		    cHtml += '<thead>'
		    cHtml += '  <tr>'
		    cHtml += '    <th class="tg-zw5y">Id: ' + Alltrim(Str(::aErro[nw,1])) + ' - Produto: ' + ::aErro[nW][2] + ' - Almoxarifado: ' + ::aErro[nW][3] + ' </th>'
		    cHtml += '  </tr>'
		    cHtml += '</thead>'
		    cHtml += '<tbody>'

        EndIf

        lFound := .T.

        cHtml += '  <tr>'
        cHtml += '    <td class="tg-0lax">' + ::aErro[nW][4] + '</td>'
        cHtml += '  </tr>'

        If nW == Len(::aErro)

            cHtml += '</tbody>'
            cHtml += '</table>'

        EndIf

    Next nW

    If lFound

        oMail:cTo := ::cEmail
        oMail:cSubject := "TBizagiIntegracaoArmazemProduto - Inconsistencias"
        oMail:cBody := cHtml

        oMail:Send()

    EndIf

Return()
