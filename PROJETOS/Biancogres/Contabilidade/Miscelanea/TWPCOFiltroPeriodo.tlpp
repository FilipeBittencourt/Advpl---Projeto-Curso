#INCLUDE "TOTVS.CH"
#INCLUDE "TOPCONN.CH"

/*/{Protheus.doc} TWPCOFiltroPeriodo
@author Wlysses Cerqueira (Facile)
@since 27/10/2020
@version 1.0
@Projet A-35
@description Consolidação empresas grupo para filial 90. 
@type class
/*/

Class TWPCOFiltroPeriodo From LongClassName

	Public Data cVersao
	Public Data cRevisa
	Public Data cAnoRef
	Public Data cMesAno
	Public Data cTipoRef
	Public Data dDataFech

	Public Data cName
	Public Data aParam
	Public Data aParRet
	Public Data bConfirm
	Public Data lConfirm

	Public Method New() ConStructor
	Public Method Pergunte()

EndClass

Method New() Class TWPCOFiltroPeriodo

	::cName := "TWPCOFiltroPeriodo"
	::aParam := {}
	::aParRet := {}
	::bConfirm := {|| .T.}
	::lConfirm := .F.

	::cVersao := SPACE(TAMSX3("ZBZ_VERSAO")[1])
	::cRevisa := SPACE(TAMSX3("ZBZ_REVISA")[1])
	::cAnoRef := SPACE(TAMSX3("ZBZ_ANOREF")[1])
	::cMesAno := Space(6)
	::dDataFech := CTOD("  /  /    ")
	::cTipoRef	:= ""

Return()

Method Pergunte(lMesAno, lDataFech, lTipoRef) Class TWPCOFiltroPeriodo

	Local lRet := .F.
	Local nTam := 1

	Default lMesAno := .F.
	Default lDataFech := .F.
	Default lTipoRef := .F.

	::bConfirm := {|| .T. }

	::aParam := {}

	::aParRet := {}

	aAdd( ::aParam, {1, "Versão"	, ::cVersao	, "@!", ".T.", "ZB5", ".T.", , .T. })
	aAdd( ::aParam, {1, "Revisão"	, ::cRevisa	, "@!", ".T.", ""	, ".T.", , .T. })
	aAdd( ::aParam, {1, "Ano Ref."	, ::cAnoRef	, "@!", ".T.", ""	, ".T.", , .T. })

	If lMesAno

		aAdd(::aParam, {1, "Mes/Ano", ::cMesAno, "@R !!/!!!!", ".T.", "", ".T.",,.F.})

	EndIf

	If lDataFech

		aAdd(::aParam, {1, "Dt fechamento", ::dDataFech, "@D", ".T.",,".T.",,.F.})

	EndIf

	If lTipoRef

		aAdd(::aParam, {1, "Tipo Ref.", ::cTipoRef, "@!", ".T.", "", ".T.",,.F.})

	EndIf

	If ParamBox(::aParam, "Operações", ::aParRet, ::bConfirm,,,,,,::cName, .T., .T.)

		lRet := .T.

		::cVersao := ::aParRet[nTam++]
		::cRevisa := ::aParRet[nTam++]
		::cAnoRef := ::aParRet[nTam++]

		If lMesAno

			::cMesAno := ::aParRet[nTam++]

		EndIf

		If lDataFech

			::dDataFech := ::aParRet[nTam++]

		EndIf

		If lTipoRef

			::cTipoRef := ::aParRet[nTam++]

		EndIf

	EndIf

Return(lRet)
