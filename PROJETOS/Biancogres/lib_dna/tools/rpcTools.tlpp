#include "totvs.ch"
/*/{Protheus.doc} RPCTools
@author Marinaldo de Jesus (Facile)
@since 07/01/2021
@version 1.0
@Projet A-35
@description RPCTools.
@type function
/*/
class RPCTools
    static method RpcSetEnv(cRpcEmp,cRpcFil,cEnvUser,cEnvPass,cEnvMod,cFunName,aTables,lShowFinal,lAbend,lOpenSX,lConnect) as logical
end class

static method RpcSetEnv(cRpcEmp,cRpcFil,cEnvUser,cEnvPass,cEnvMod,cFunName,aTables,lShowFinal,lAbend,lOpenSX,lConnect) class RPCTools
	local cUserID 	as character
	local lRet		as logical
	local lIsBlind	as logical
	if (type("__cUserID")=="C")
		cUserID:=&("__cUserID")
	endif
	lIsBlind:=IsBlind()
	if (!type("cEmpAnt")=="C")
        private cEmpAnt:="__cEmpAnt__"
    endif
	if (!type("cFilAnt")=="C")
        private cFilAnt:="__cFilAnt__"
    endif
    if ((!&("cEmpAnt")==cRpcEmp).or.(!&("cFilAnt")==cRpcFil))
		DEFAULT cRpcEmp:=&("cEmpAnt")
		DEFAULT cRpcFil:=&("cFilAnt")
		DEFAULT cEnvMod:=if((type("cModulo")=="C"),&("cModulo"),cEnvMod)
		DEFAULT cFunName:=FunName()
        RpcClearEnv()
        lRet:=RpcSetEnv(@cRpcEmp,@cRpcFil,@cEnvUser,@cEnvPass,@cEnvMod,@cFunName,@aTables,@lShowFinal,@lAbend,@lOpenSX,@lConnect)
        DEFAULT lRet:=((Select("SX6")>0).and.(&("cEmpAnt")==cRpcEmp))
		if (lRet)
            if !(&("cEmpAnt")==cRpcEmp)
                &("cEmpAnt"):=cRpcEmp
		    endif
            if !(&("cFilAnt")==cRpcFil)
                &("cFilAnt"):=cRpcFil
            endif
        endif
		if (!lIsBlind)
			SetsDefault()
			if (type("__cInternet")=="C")
				&("__cInternet"):=nil
			endif
		endif
		__SetCentury("ON")
	endif
	if (!empty(cUserID))
		&("__cUserID"):=cUserID
	endif
	return(lRet)
