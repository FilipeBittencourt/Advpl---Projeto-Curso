#INCLUDE "TOTVS.CH"
#INCLUDE "TOPCONN.CH"

/*/{Protheus.doc} TProcess
@author Fernando Rocha
@since 17/06/2019
@project Automação
@version 1.0
@description Classe para gerenciamento de processos
@type class
/*/

Class TProcess From LongClassName

	Public Data cIDProc
	Public Data oWFP
	Public Data lAviso
	
	Public Method New() Constructor
	Public Method GetNewID()
	Public Method Start()
	Public Method Finish()
	Public Method SetConsoleLog(cPar)
	
EndClass


Method New() Class TProcess
	
	::lAviso := .F.	
	::cIDProc := ""	
	::oWFP := TWorkFlowProcess():New()
	 	
Return()


Method GetNewID() Class TProcess
Local cRet := ""
Local nOrdem := 2
Local cChaveID := ""

	cRet := GetSxENum("ZK2", "ZK2_IDPROC", "ZK2_IDPROC", nOrdem)
	
	cChaveID := cRet
		
	DbSelectArea("ZK2")
	ZK2->(DbSetOrder(nOrdem))
	
	While ZK2->(MsSeek(cChaveID))
		
		If __lSx8
		
			ConfirmSX8()
		
		EndIf
		
		cRet := GetSxENum("ZK2", "ZK2_IDPROC", "ZK2_IDPROC", nOrdem)
		
		cChaveID := cRet
		
	EndDo

	ConfirmSx8()

Return(cRet)


Method Start() Class TProcess
	
	::cIDProc := ::GetNewID()	
	::SetConsoleLog("I")
	
Return(::cIDProc)


Method Finish() Class TProcess
		
	::oWFP:cIDProc := ::cIDProc
	::oWFP:lAviso := ::lAviso
	::oWFP:Send()		
	::SetConsoleLog("F")
	
Return()


Method SetConsoleLog(cPar) Class TProcess
Local cLog := ""
Local cTit := ""

	If cPar == "I"

		cTit := "Iniciado"

	ElseIf cPar == "F"

		cTit := "Finalizado"
		
	EndIf

	cLog := Replicate("-", 120) + Chr(13)
	cLog += "[" + Dtoc(Date()) + Space(1) + Time() + "] -- TProcess [LOG DE PROCESSO] -- [Empresa: "+ cEmpAnt +"] -- [Filial: "+ cFilAnt +"] -- [Processo: " + ::cIDProc + "] -- " + cTit + Chr(13)
	cLog += Replicate("-", 120)
	
	ConOut(Chr(13) + cLog)

Return()